# Reader for the matrix market exchange coodinate format

import strutils
import strformat
import streams
import strscans

import edge
import edge_stream
import logger


type
    MtxStream* = ref object of EdgeStream
        rows*: int
        cols*: int
        entries*: int
        entries_pos: int
        num_edges_written: int

method readEdge *(s: MtxStream, edge: var Edge): bool =
    # start reading after the comments/header
    let datapos = s.entries_pos
    if s.getPosition() < data_pos:
        debug(&"set position past header: {datapos}")
        s.setPosition(datapos)
    for line in s.stream.lines():
        if line.strip() == "":
            debug("skip empty line")
            continue
        else:
            var
                src, dst: int
                weight: float
            debug("parsing line: ", line)
            if scanf(line, "$s$i$s$i$s$f", src, dst, weight):
                assert src <= s.rows
                assert dst <= s.cols
                edge = initEdge(src-1, dst-1, weight)
                return true
            elif not s.atEnd():
                error("error parsing line ", line)
                return false
    return false


method writeEdge(s: MtxStream, edge: Edge) =
    let
        src = edge.src + 1
        dst = edge.dst + 1
        weight = edge.weight
    assert src <= s.rows
    assert dst <= s.cols, &"edge with col {dst} is outside matrix {s.cols}"
    assert s.num_edges_written < s.entries, &"wrote more edges {s.num_edges_written} than expected {s.entries}"
    s.stream.writeLine($src & "\t" & $dst & "\t" & $weight)
    s.num_edges_written += 1

proc newMtxStream(stream: Stream): MtxStream =
    new(result)
    result.stream = stream


proc newMtxWriter *(stream: Stream, rows, cols, entries: int): MtxStream =
    result = newMtxStream(stream)
    result.rows = rows
    result.cols = cols
    result.entries = entries
    result.stream.writeLine("% generated by cwpearson/graph-datasets2")
    result.stream.writeLine(&"{result.rows} {result.cols} {result.entries}")
    result.num_edges_written = 0


proc openMtxWriter *(path: string, rows, cols, entries: int): MtxStream =
    var stream = openFileStream(path, fmWrite)
    result = newMtxWriter(stream, rows, cols, entries)





proc newMtxReader *(stream: Stream): MtxStream =
    result = newMtxStream(stream)
    for line in result.stream.lines():
        debug(&"current line again: {line}")
        # skip comment lines
        if line.startsWith("%"):
            debug("skip comment line: ", line)
            continue
        # read the first line to get weights
        debug("getting rows,cols,entries from line: ", line)
        assert scanf(line, "$s$i$s$i$s$i", result.rows, result.cols,
                result.entries)
        result.entries_pos = result.stream.getPosition()
        break

proc openMtxReader *(path: string): MtxStream =
    var stream = openFileStream(path, fmRead)
    result = newMtxReader(stream)

when isMainModule:
    let contents = """%test  data
    1 2 3
    1   1   1.0
    1   2   2.0
    """
    var stream = newStringStream(contents)
    var reader = newMtxReader(stream)
    for edge in reader.edges():
        echo edge

    var ostream = newStringStream()
    var writer = newMtxWriter(ostream, 1, 2, 2)
    reader.setPosition(0)
    for edge in reader.edges():
        echo edge
        writer.writeEdge(edge)

    echo ostream.data
